pipeline {

    agent {
        label 'linux'
    }

    options {
        timeout(time: 45, unit: 'MINUTES')
    }

    environment {
        VAULT_ADDR = credentials('vault-addr')
        VAULT_ROLE_ID = credentials('vault-role-id')
        VAULT_SECRET_ID = credentials('vault-secret-id')
        GCLOUD_PROJECT = credentials('k8s-operators-gcloud-project')
    }

    stages {
        stage('Check if Docker image needs rebuilding') {
            when {
                expression {
                    notOnlyDocs()
                }
            }
            steps {
                sh 'make -C build/ci ci-build-image'
            }
        }
        stage('Run tests in parallel') {
            parallel {
                stage("Run unit and integration tests") {
                    when {
                        expression {
                            checkout scm
                            notOnlyDocs()
                        }
                    }
                    agent {
                        label 'linux'
                    }
                    steps {
                        createConfig()
                        script {
                            env.SHELL_EXIT_CODE = sh(returnStatus: true, script: 'make -C build/ci TARGET=ci ci')

                            sh 'TEST_OUTPUT_XML=unit-tests.xml TEST_OUTPUT_JSON=unit-tests.json make -C build/ci TARGET=generate-test-xml ci'
                            junit "unit-tests.xml"

                            sh 'TEST_OUTPUT_XML=integration-tests.xml TEST_OUTPUT_JSON=integration-tests.json make -C build/ci TARGET=generate-test-xml ci'
                            junit "integration-tests.xml"

                            sh 'exit $SHELL_EXIT_CODE'
                        }
                    }
                }
                stage("Run smoke E2E tests") {
                    when {
                        expression {
                            checkout scm
                            notOnlyDocs()
                        }
                    }
                    steps {
                        createConfig()
                        sh """
                            cat >deployer-config.yml <<EOF
id: gke-ci
overrides:
  kubernetesVersion: "1.12"
  clusterName: $BUILD_TAG
  vaultInfo:
    address: $VAULT_ADDR
    roleId: $VAULT_ROLE_ID
    secretId: $VAULT_SECRET_ID
  gke:
    gCloudProject: $GCLOUD_PROJECT
EOF
                        """
                        script {
                            env.SHELL_EXIT_CODE = sh(returnStatus: true, script: 'make -C build/ci TARGET=ci-e2e ci')

                            sh 'TEST_OUTPUT_XML=e2e-tests.xml TEST_OUTPUT_JSON=e2e-tests.json make -C build/ci TARGET=generate-test-xml ci'
                            junit "e2e-tests.xml"

                            sh 'exit $SHELL_EXIT_CODE'
                        }
                    }
                }
            }
        }
    }

    post {
        cleanup {
            script {
                if (notOnlyDocs()) {
                    build job: 'cloud-on-k8s-e2e-cleanup',
                        parameters: [string(name: 'GKE_CLUSTER', value: "${BUILD_TAG}")],
                        wait: false
                }
            }
            cleanWs()
        }
    }
}

def notOnlyDocs() {
    // grep succeeds if there is at least one line without docs/
    return sh (
        script: "git diff --name-status HEAD~1 HEAD | grep -v docs/",
    	returnStatus: true
    ) == 0
}

void createConfig() {
    sh """
        cat >.env <<EOF
REGISTRY = eu.gcr.io
REPOSITORY = "$GCLOUD_PROJECT"
TESTS_MATCH = TestSmoke
SKIP_DOCKER_COMMAND = false
IMG_SUFFIX = -ci
EOF
    """
}
